varnishtest "Check timeout_idle"

varnish v1 -arg "-p timeout_idle=1"	\
	   -arg "-a 127.0.0.1:0"	\
	   -arg "-a ${tmpdir}/v1.sock"	\
	   -vcl {
	backend dummy { .host = "${bad_backend}"; }

	sub vcl_deliver {
		if (req.url == "/sess") {
			set sess.timeout_idle = 2s;
		}
	}
	sub vcl_backend_error {
		set beresp.status = 200;
		set beresp.ttl = 1h;
	}
} -start

client c1 {
	txreq
	rxresp
	delay 0.2
	txreq
	rxresp
	delay 1.2
	expect_close
} -start

client c2 {
	txreq -url "/sess"
	rxresp
	delay 1.2
	txreq
	rxresp
	delay 2.2
	expect_close
} -start

client c1u -connect "${tmpdir}/v1.sock" {
	txreq
	rxresp
	delay 0.2
	txreq
	rxresp
	delay 1.2
	expect_close
} -start

client c2u -connect "${tmpdir}/v1.sock" {
	txreq -url "/sess"
	rxresp
	delay 1.2
	txreq
	rxresp
	delay 2.2
	expect_close
} -start

client c1 -wait
client c2 -wait
client c1u -wait
client c2u -wait
